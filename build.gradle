buildscript {
    ext.junit_version = "4.13.2"
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

group 'uk.co.mainwave.mimetypes'
version getVersionFromGit()

sourceCompatibility = JavaVersion.VERSION_1_8

dependencies {
    testImplementation "junit:junit:$junit_version"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/markwhitaker/mimetypes.java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

def getVersionFromGit() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "describe", "--tags", "--always"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return "1.0.0"
    }
}